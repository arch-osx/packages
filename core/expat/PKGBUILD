pkgname=expat
pkgver=2.2.1
pkgrel=1
pkgdesc='An XML parser library'
arch=('x86_64')
url='https://libexpat.github.io'
license=('custom')
depends=('libc')
source=(
  http://downloads.sourceforge.net/sourceforge/expat/$pkgname-$pkgver.tar.bz2
  fix_getrandom.patch
)
md5sums=(
  'd9c3baeab58774cefc2f04faf29f2cf8'
  '6d4101ae7ca51b1f07358feabe8710c4'
)

prepare() {
  cd $pkgname-$pkgver
  # Upstream commit from 18 Jun 2017 "configure.ac: Fix mis-detection of
  # getrandom on Debian GNU/kFreeBSD (#50)"
  # See https://github.com/libexpat/libexpat/commit/602e6c78ca750c082b72f8cdf4a38839b312959f
  patch -p1 -i ../fix_getrandom.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/opt/arch
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir"/opt/arch/share/licenses/$pkgname
  install -m644 COPYING "$pkgdir"/opt/arch/share/licenses/$pkgname/COPYING
}
