pkgname=readline
_basever=7.0
_patchlevel=003
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc='GNU readline library'
arch=('x86_64')
url='http://tiswww.case.edu/php/chet/readline/rltop.html'
license=('GPL')
depends=()
backup=()
options=('!emptydirs')
install=readline.install
source=(http://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz{,.sig})
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

if [ $_patchlevel -gt 0 ]; then
    for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//.}-$(printf "%03d" $_p){,.sig})
    done
fi

prepare() {
  cd $pkgname-$_basever
  for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
    msg "applying patch readline${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../readline${_basever//.}-$(printf "%03d" $_p)
  done

  ## remove RPATH from shared objects (FS#14366)
  #sed -i~ -e 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf
}

build() {
  cd $pkgname-$_basever

  # build with -fPIC for x86_64 (FS#15634)
  [[ $CARCH == "x86_64" ]] && CFLAGS="$CFLAGS -fPIC"

  ./configure --prefix=/opt/arch
  make SHLIB_LIBS=-lncurses
}

package() {
  make -C $pkgname-$_basever DESTDIR="$pkgdir" install
}

md5sums=('205b03a87fc83dab653b628c59b9fc91'
         'SKIP'
         'e299384458a4cbefaaac3f30e9cc2bba'
         'SKIP'
         'f9071a353e2fd52a91d32667b23715d6'
         'SKIP'
         '03595464cf0283286a6e07f4f01c4a70'
         'SKIP'
)
